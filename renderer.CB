Const TW=16
Const TH=8
Const SW=200
Const SH=150
SCREEN SW*4,SH*4

Const ENTITY_PARTICLE = 0
Const ENTITY_COPTER = 1

Type ENTITIES
    // Generic
    Field etype
    Field t
    Field tx As Float
    Field ty As Float
	Field z As Float
    Field l
    Field wayPoint
    Field start
    // Particles
    Field xv As Float
    Field yv As Float
    Field zv As Float
    Field xa As Float
    Field ya As Float
    Field za As Float
    Field f
    // Copter
    Field tank#
EndType

Type WAYPOINTS
    Field tx
    Field ty
    Field n
    Field lx
    Field ly
EndType

Dim Tiles(TILE_COUNT,4)
Dim flames(2,4)
Dim copter(3,1)
Dim flags(3)
loadtiles()
loadflames()
loadcopter()
loadFlags()

// Load particle images
Const pFrames = 32
Const PARTICLE_FIRE = 0
Const PARTICLE_SMOKE = 1
Const PARTICLE_FOAM = 2
Dim pColors(2,pFrames)

Const COPTER_FUEL_USAGE = 1.0
Const COPTER_SPEED = 10.0

pImg = LoadImage("gfx/tuli_partikkeli.png")
DrawToImage pImg
For i=0 To pFrames
    pColors(PARTICLE_FIRE,i)=GetPixel(i,0)
Next i
pImg = LoadImage("gfx/savu_partikkeli.png")
DrawToImage pImg
For i=0 To pFrames
	pColors(PARTICLE_SMOKE,i)=GetPixel(i,0,Image(pImg))
Next i
pImg = LoadImage("gfx/vaahto_partikkeli.png")
DrawToImage pImg
For i=0 To pFrames
	pColors(PARTICLE_FOAM,i)=GetPixel(i,0,Image(pImg))
Next i

DrawToScreen

Goto skipRendering

drawLoading:
Color cbwhite
	render = True
    DrawToScreen
    iterations# = 105
    cx=100
    cy=-325
    For i=0 To Int(iterations)
        GrowTrees()
        // Render loading animation
        If i Mod 10 = 1 And render Then
            cx = cx + SW*1.5 : cy = cy + SH*1.5
            For tx=0 To MAP_WIDTH-1
                For ty=0 To MAP_HEIGHT-1
                    id = mapTiles(tx,ty)
                    Gosub drawTile
                Next ty
            Next tx
            cx = cx - SW*1.5 : cy = cy - SH*1.5
            Color cbblack
            CenterText 401,315,"Growing trees..."
            Box 399-(i/iterations*800)/2,289,i/iterations*800+2,22,1
            Color cbwhite
            CenterText 400,314,"Growing trees..."
            Color 255-i/iterations*200,255,255
            Box 400-(i/iterations*800)/2,290,i/iterations*800,20,1
            DrawScreen
        EndIf
    Next i
Return

drawwaypoints:
	Color cbwhite
	For wp.WAYPOINTS = Each WAYPOINTS
		tx = wp\tx
		ty = wp\ty
		Gosub kamera
		wpx = sx
		wpy = sy
		DrawImage flags(0), sx, sy - 12
		If wp\lx <> wp\tx And wp\ly <> wp\ty Then
			tx = wp\lx
			ty = wp\ly
			Gosub kamera
			Color 219, 219, 0
			Line sx,sy,wpx+2,wpy-1
			a = GetAngle(sx,sy,wpx,wpy)
			Line sx,sy,sx+Cos(a+16)*5,sy-Sin(a+16)*5
			Line sx,sy,sx+Cos(a-16)*5,sy-Sin(a-16)*5
		Else
			If makingWP = ConvertToInteger(wp) Then
				sx = MouseX() / 4
				sy = MouseY() / 4
				Color 219, 219, 0
				Line sx,sy,wpx+2,wpy-1
				a = GetAngle(sx,sy,wpx,wpy)
				Line sx,sy,sx+Cos(a+16)*5,sy-Sin(a+16)*5
				Line sx,sy,sx+Cos(a-16)*5,sy-Sin(a-16)*5
			EndIf
		EndIf
	Next wp
Return

drawTile:
	Gosub kamera
	
	istree=id<6 And id>2
	If heat(tx,ty) < -5 Then
		If Tiles(id,2) > 0 Then DrawImage Tiles(id,2),sx,sy-ImageHeight(Tiles(id,2))
	Else
		If Tiles(id,burning) > 0 Then DrawImage Tiles(id,burning),sx,sy-ImageHeight(Tiles(id,burning))
		If heat(tx,ty) > 0 Then
			If (Sqrt(heat(tx,ty))+istree*5)>22 Then DrawImage flames(1,Rand(3)),sx,sy-ImageHeight(flames(1,0))
			If (Sqrt(heat(tx,ty))+(id=TILE_HAY)*10)>18 Then DrawImage flames(2,Rand(3)),sx,sy-ImageHeight(flames(2,0))
			If Rand(Sqrt(heat(tx,ty)))>7 Then DrawImage flames(0,Rand(3)),sx,sy-ImageHeight(flames(0,0))
		EndIf
	EndIf
Return

kamera:
	sx=(tx-(ty+2))/2.0*TW	+cx + TW * 0.5
	sy=(tx+(ty+2))/2.0*TH	+cy
Return

kamerafloat:
	sxf#=(txf#-(tyf#+2))/2.0*TW	+cx + TW * 0.5
	syf#=(txf#+(tyf#+2))/2.0*TH	+cy
Return

inversekamera:
	tx = (sx*TH+sy*TW-cx*TH-cy*TW)/(TW*TH)
	ty = (-sx*TH+sy*TW+cx*TH-cy*TW)/(TW*TH)

Return

skipRendering:

Function makeParticle(t,x,y,z,xv#,yv#,zv#,xa#,ya#,za#,f,l)
    p.ENTITIES = New(ENTITIES)
    p\etype = ENTITY_PARTICLE
    p\t=t
	p\tx=x+0.5+ Rnd(-0.3,0.3)
	p\ty=y+0.5+ Rnd(-0.3,0.3)
	p\z=z + Rnd(-0.3,0.3)
	p\xv=xv + Rnd(-0.4,0.4)
	p\yv=yv + Rnd(-0.4,0.4)
	p\zv=zv
	p\xa=xa + Rnd(-4,4)
	p\ya=ya + Rnd(-4,4)
	p\za=za
	p\f=f
	p\l=l
	p\start=Timer()
EndFunction

Function loadcopter()
	varjo=LoadImage("gfx/kopteri_varjo.png")
	runko=LoadImage("gfx/kopteri_runko.png")
	makecopter(varjo,0)
	makecopter(runko,1)
EndFunction

Function loadflames()
	img=LoadImage("gfx/tuli.png")
	For i=0 To 3
		flames(0,i)= maketile(img,i)
		flames(1,i)= maketile(img,i+4)
		flames(2,i)= maketile(img,i+8)
	Next i
EndFunction

Function loadFlags()
	img=LoadImage("gfx/waypoint_lippu.png")
	For i=0 To 3
		flags(i) = makeFlag(img,i)
	Next i
EndFunction

Function loadtiles()
	//Ground
	img=LoadImage("gfx/perusmaa.png")
	img2=LoadImage("gfx/perusmaa_kaste.png")
	smoldering=maketile(img,6)
	Tiles(TILE_EMPTY,0)=maketile(img,0)
	Tiles(TILE_EMPTY,1)=smoldering
	Tiles(TILE_EMPTY,2)=maketile(img2,0)
	
	Tiles(TILE_GRASS,0)=maketile(img,1)
	Tiles(TILE_GRASS,1)=smoldering
	Tiles(TILE_GRASS,2)=maketile(img2,1)
	
	Tiles(TILE_HAY,0)=maketile(img,2)
	'smoldering defined later
	Tiles(TILE_HAY,2)=maketile(img2,2)
	
	//Tiles(TILE_HAY,1)=smoldering
	Tiles(TILE_BURNED_GRASS,0)=maketile(img,5)
	Tiles(TILE_BURNED_GRASS,1)=smoldering
	Tiles(TILE_BURNED_GRASS,2)=maketile(img2,5)
	
	Tiles(TILE_ROAD,0)=maketile(img,3)
	Tiles(TILE_ROAD,1)=maketile(img,3)
	Tiles(TILE_ROAD,2)=maketile(img2,3)
	
	Tiles(TILE_WATER,0)=maketile(img,4)
	Tiles(TILE_WATER,1)=maketile(img,4)
	Tiles(TILE_WATER,2)=maketile(img,4)
	
	//Trees
	img=LoadImage("gfx/puu_isokuusi.png")
	img2=LoadImage("gfx/puu_isokuusi_kaste.png")
	Tiles(TILE_TREE1,0)=maketile(img,0)	
	Tiles(TILE_TREE1,1)=maketile(img,1)	
	Tiles(TILE_TREE1,2)=maketile(img2,0)	
	
	Tiles(TILE_BURNED_TREE,0)=maketile(img,3)	
	Tiles(TILE_BURNED_TREE,1)=maketile(img,4)
	
	Tiles(TILE_HAY,1)=maketile(img,4)
	
	img=LoadImage("gfx/puu_isokuusi2.png")
	img2=LoadImage("gfx/puu_isokuusi2_kaste.png")
	
	Tiles(TILE_TREE2,0)=maketile(img,0)
	Tiles(TILE_TREE2,1)=maketile(img,1)	
	Tiles(TILE_TREE2,2)=maketile(img2,0)
	
	img=LoadImage("gfx/puu_isokuusi3.png")
	img2=LoadImage("gfx/puu_isokuusi3_kaste.png")
	
	
	Tiles(TILE_TREE3,0)=maketile(img,0)
	Tiles(TILE_TREE3,1)=maketile(img,1)	
	Tiles(TILE_TREE3,2)=maketile(img2,0)	
	
EndFunction

Function maketile(img,frame)
	h=ImageHeight(img)
	i=MakeImage(16,h)
	CopyBox frame*16,0,16,h,0,0,Image(img),Image(i)
	Return i
EndFunction

Function makecopter(img,ii)
	w = ImageWidth(img)/4
	h = ImageHeight(img)
	For i = 0 To 3
		copter(i,ii)=MakeImage(w,h)
		CopyBox w*i,0,w,h,0,0,Image(img),Image(copter(i,ii))
	Next i
EndFunction

Function makeFlag(img,frame)
	h=ImageHeight(img)
	i=MakeImage(12,h)
	CopyBox frame*12,0,12,h,0,0,Image(img),Image(i)
	Return i
EndFunction

